
# Send Information to the Clipboard

## Use the RunCommand Method

You can use the  **[RunCommand](5D4A4A3C-CEA0-7F2C-8AF7-51B65F7BDCF8.md)** method with the **acCmdCopy** constant to copy the contents of the active control on a form or report to the Clipboard. The following example illustrates how to copy the contents of a text box named txtNotes to the Clipboard.


```
Private Sub cmdCopy_Click() 
   Me!txtNotes.SetFocus 
   DoCmd.RunCommand acCmdCopy 
End Sub
```


## Use the Windows API

To use Windows API calls to send information to the Clipboard, paste the following code into the Declarations section of a standard module.


```
#If VBA7 Then
Declare PtrSafe Function GlobalUnlock Lib "kernel32" (ByVal hMem As Long) As Long
#Else
Declare Function GlobalUnlock Lib "kernel32" (ByVal hMem As Long) As Long
#End If

#If VBA7 Then
Declare PtrSafe Function GlobalLock Lib "kernel32" (ByVal hMem As Long) As Long
#Else
Declare Function GlobalLock Lib "kernel32" (ByVal hMem As Long) As Long
#End If

#If VBA7 Then
Declare PtrSafe Function GlobalAlloc Lib "kernel32" (ByVal wFlags As Long, ByVal dwBytes As Long) As Long
#Else
Declare Function GlobalAlloc Lib "kernel32" (ByVal wFlags As Long, ByVal dwBytes As Long) As Long
#End If

#If VBA7 Then
Declare PtrSafe Function CloseClipboard Lib "User32" () As Long
#Else
Declare Function CloseClipboard Lib "User32" () As Long
#End If

#If VBA7 Then
Declare PtrSafe Function OpenClipboard Lib "User32" (ByVal hwnd As Long) As Long
#Else
Declare Function OpenClipboard Lib "User32" (ByVal hwnd As Long) As Long
#End If

#If VBA7 Then
Declare PtrSafe Function EmptyClipboard Lib "User32" () As Long
#Else
Declare Function EmptyClipboard Lib "User32" () As Long
#End If

#If VBA7 Then
Declare PtrSafe Function lstrcpy Lib "kernel32" (ByVal lpString1 As Any, ByVal lpString2 As Any) As Long
#Else
Declare Function lstrcpy Lib "kernel32" (ByVal lpString1 As Any, ByVal lpString2 As Any) As Long
#End If

#If VBA7 Then
Declare PtrSafe Function SetClipboardData Lib "User32" (ByVal wFormat As Long, ByVal hMem As Long) As Long
#Else
Declare Function SetClipboardData Lib "User32" (ByVal wFormat As Long, ByVal hMem As Long) As Long
#End If
 
Public Const GHND = &amp;H42 
Public Const CF_TEXT = 1 
Public Const MAXSIZE = 4096
```

The following procedure illusrtrates how to send information to the Clipboard.




```
Function ClipBoard_SetData(MyString As String) 
   Dim hGlobalMemory As Long, lpGlobalMemory As Long 
   Dim hClipMemory As Long, X As Long 
 
   ' Allocate moveable global memory. 
   '------------------------------------------- 
   hGlobalMemory = GlobalAlloc(GHND, Len(MyString) + 1) 
 
   ' Lock the block to get a far pointer 
   ' to this memory. 
   lpGlobalMemory = GlobalLock(hGlobalMemory) 
 
   ' Copy the string to this global memory. 
   lpGlobalMemory = lstrcpy(lpGlobalMemory, MyString) 
 
   ' Unlock the memory. 
   If GlobalUnlock(hGlobalMemory) <> 0 Then 
      MsgBox "Could not unlock memory location. Copy aborted." 
      GoTo OutOfHere2 
   End If 
 
   ' Open the Clipboard to copy data to. 
   If OpenClipboard(0&amp;) = 0 Then 
      MsgBox "Could not open the Clipboard. Copy aborted." 
      Exit Function 
   End If 
 
   ' Clear the Clipboard. 
   X = EmptyClipboard() 
 
   ' Copy the data to the Clipboard. 
   hClipMemory = SetClipboardData(CF_TEXT, hGlobalMemory) 
 
OutOfHere2: 
 
   If CloseClipboard() = 0 Then 
      MsgBox "Could not close Clipboard." 
   End If 
 
   End Function
```

